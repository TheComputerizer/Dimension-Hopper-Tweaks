pluginManagement {
    repositories {
        gradlePluginPortal()
        [ 'forge', 'fancy', 'sponge' ].forEach {maven_id ->
            maven {
                name = "${settings.ext."${maven_id}_name"}"
                url = uri "${settings.ext."${maven_id}_uri"}"
                content {
                    "${settings.ext."${maven_id}_groups"}".split(';').each {grp ->
                        includeGroup grp
                    }
                }
            }
        }
    }
    resolutionStrategy {
        eachPlugin {
            if(requested.id.toString()==mixingradle_id)
                useModule "$mixingradle_module:${requested.version}"
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '+'
}

rootProject.name = project_name

include ':mixins'
findProject(':mixins')?.name = 'mixins'

dependencyResolutionManagement {
    versionCatalogs {
        create("$deps_id") {
            from(files(deps_path))
        }
    }
}