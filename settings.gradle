pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            name = 'MinecraftForge'
            url = uri 'https://maven.minecraftforge.net/'
            content {
                [ 'net.minecraftforge', 'net.minecraftforge.gradle' ].forEach{ grp ->
                    includeGroup grp
                }
            }
        }
        maven {
            name = 'Garden of Fancy'
            url = uri 'https://maven.gofancy.wtf/releases'
            content {
                includeGroup 'wtf.gofancy.fancygradle'
            }
        }
        maven {
            name = 'SpongePowered'
            url = uri 'https://repo.spongepowered.org/repository/maven-public/'
            content {
                includeGroup 'org.spongepowered'
            }
        }
    }
    resolutionStrategy {
        eachPlugin {
            if(requested.id.toString()=='org.spongepowered.mixin') {
                useModule "org.spongepowered:mixingradle:${requested.version}"
            }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '+'
}

rootProject.name = 'Dimension_Hopper_Tweaks'

[ ':mixins' ].forEach {
    include it
    findProject(it)?.name = it.substring(1)
}